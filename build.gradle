plugins {
    id 'groovy'
    id 'idea'
    id 'application'
    id 'codenarc'
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile 'log4j:log4j:1.2.+'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.+'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.apache.commons:commons-math3:3.+'
}

mainClassName = 'NoClassName'
['runPublishNotify', 'runTests', 'prePublish', 'publishToCouch',
    'publishToInfinity', 'publishToJira', 'publishToHipchat', 'emailCycleResults'].each { name ->
    task "${name}StartScripts"(type: CreateStartScripts) {
        mainClassName = name.capitalize()
        applicationName = name
        outputDir = new File('src/dist/bin')
        classpath = startScripts.classpath
        defaultJvmOpts = ['-Dlog4j.configuration=file:conf/log4j.xml']
        startScripts.dependsOn "${name}StartScripts"

        // The classpath generated in the .bat script lists all the jars.
        // This shortens it using a wildcard
        // Needed to prevent "input line is too long" errors running from windows cmd shell
        doLast {
            def scriptFile = file "${outputDir}/${name}.bat"
            scriptFile.text = scriptFile.text.replaceFirst(/(set CLASSPATH=.*?\\lib\\).*/,/$1*/)
        }
    }
}

version = Boolean.parseBoolean(System.properties['release']) ? '1.0.0' : '2.0.0-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'paradox-publisher'
        }
    }
    repositories { maven { url "$buildDir/repo"} }
}

if (project.hasProperty('artifactoryPublish')) {
    artifactory {
        publish {
            repository {
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishPom = true
            }
        }
    }
}

codenarc {
    toolVersion = '1.0'
    configFile = new File('conf/codenarc.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 0
    reportFormat = project.properties.'codenarc.reportFormat' ?: 'html'
}

